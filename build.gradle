plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'
	id 'com.palantir.docker' version '0.21.0'
	id 'com.palantir.docker-run' version '0.21.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'club.javacloud'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

docker {
	name "${project.group}/${bootJar.baseName}"

	copySpec.from(tasks.bootJar.outputs.files.singleFile).into("/")
	copySpec.rename(tasks.bootJar.outputs.files.singleFile.name, "app.jar")

	dockerfile file('docker/prod/Dockerfile')
}

dockerRun {
	name "${bootJar.baseName}-container"
	image "${project.group}/${bootJar.baseName}"
	//volumes '/tmp':'/tmp'
	ports   '5000:8080',
			'5005:5005',
			'1898:1898',
			'16000:16000'
	command "java",
			"-agentpath:/usr/local/jprofiler10.1.5/bin/linux-x64/libjprofilerti.so=port=16000,nowait",
			"-Xdebug",
			"-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005",
			"-Dcom.sun.management.jmxremote.port=1898",
			"-Dcom.sun.management.jmxremote.rmi.port=1898",
			"-Dcom.sun.management.jmxremote.ssl=false",
			"-Dcom.sun.management.jmxremote.authenticate=false",
			"-Djava.rmi.server.hostname=127.0.0.1",
			"-Djava.security.egd=file:/dev/./urandom",
			"-jar",
			"/app.jar"
}
